**Вариант 4-113311**

----------------------------------------------------------------------------------------------

<ключевое_слово> ::= or | and | not | true | false | program | var | begin | end. |
int | float | bool | as | if | then | else | for | to | do | while | read | write

<разделитель> ::= <> | = | < | <= | > | >= | + | - | * | / | ( | ) |
 ; | , | [ | ] | : | { | }

----------------------------------------------------------------------------------------------

<идентификатор>::= <буква> {<буква> | <цифра>}

<буква>::= A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T |
 U | V | W | X | Y | Z | a | b | c | d | e | f | g | h | i | j | k | l | m | n | o | p
 q | r | s | t | u | v | w | x | y | z

<цифра>::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

----------------------------------------------------------------------------------------------

<программа>::= program var <описание> begin <оператор> {; <оператор>} end.
**<программа>::= program var <описание> {; <описание>} begin <оператор> {; <оператор>} end.**

<описание>::= <тип> <идентификатор> {, <идентификатор>}

<тип>::= int | float | bool


<оператор>::= <составной> | <присваивания> | <условный> | <фиксированного_цикла> |
<условного_цикла> | <ввода> | <вывода>

<составной>::= «[» <оператор> {( : | перевод строки) <оператор>} «]»

<присваивания>::= <идентификатор> as <выражение>

<условный>::= if <выражение> then <оператор> [ else <оператор>]

<фиксированного_цикла>::= for <присваивания> to <выражение> do <оператор>

<условного_цикла>::= while <выражение> do <оператор>

<ввода>::= read «(»<идентификатор> {, <идентификатор> } «)»

<вывода>::= write «(»<выражение> {, <выражение> } «)»

<комментарий>::= «{» \w* «}»

----------------------------------------------------------------------------------------------

<выражение>::= <операнд>{<операции_группы_отношения> <операнд>}

<операнд>::= <слагаемое> {<операции_группы_сложения> <слагаемое>}

<слагаемое>::= <множитель> {<операции_группы_умножения> <множитель>}

<множитель>::= <идентификатор> | <число> | <логическая_константа> |
<унарная_операция> <множитель> | «(»<выражение>«)»

<число>::= <целое> | <действительное>

<логическая_константа>::= true | false

<операции_группы_отношения>:: = <> | = | < | <= | > | >=

<операции_группы_сложения>:: = + | - | or

<операции_группы_умножения>::= * | / | and

<унарная_операция>::= not

----------------------------------------------------------------------------------------------

<целое>::= <двоичное> | <восьмеричное> | <десятичное> | <шестнадцатеричное>

<двоичное>::= {/ 0 | 1 /} (B | b)

<восьмеричное>::= {/ 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 /} (O | o)

<десятичное>::= {/ <цифра> /} [D | d]

<шестнадцатеричное>::= <цифра> {<цифра> | A | B | C | D | E | F | a | b | c | d | e | f} (H | h)

<действительное>::= <числовая_строка> <порядок> |[<числовая_строка>].<числовая_строка> [порядок]

<числовая_строка>::= {/ <цифра> /}

<порядок>::= (E | e)[+ | -] <числовая_строка>